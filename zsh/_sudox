#compdef sudox sudoxe
# SPDX-License-Identifier: BSD-3-Clause
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
local opts
opts=()
case $service in
(*sudox)
	opts=(
'-l[Clear TMUX/STY]'
'-e[Check for clear TMUX/STY environment]'
);;
esac
_arguments -C -s -S -A '-*' : $opts \
'-v+[Use specified VAR/cmdline for cookie passing]:variable:->var' \
'-F+[Use specified FD/FIFO for cookie passing]:file descriptor:->fd' \
'-l[Clear STY/TMUX]' \
'-s[Start a tmux/screen session]' \
'-S[Start a screen/tmux session]' \
'-e[Require empty STY/TMUX]' \
'-p[Ignore ASKPASS]' \
'-O[keep/restore original HOME]' \
'-H[Update HOME (default)]' \
'-T[Do not call script - danger when interactive]' \
'(-R -N)-u[Generate only an untrusted permission]' \
'-t+[Use specified time for untrusted permission]:time:(0)' \
'(-u -t)-U[Delete X remote authority similar to -ut1]' \
'(-u -t -X)-x[Do not transfer X cookies, DISPLAY, SHELL]' \
'(-u -t)-X[Do not transfer X cookies]' \
'-R[Force root mode]' \
'-N[Force non-root mode]' \
'*-o+[Pass specified option to sudo]:sudo option:()' \
'-A[sudo --askpass]' \
'-b[sudo --background]' \
'-E[sudo --preserve-env]' \
'-i[sudo --login]' \
'-K[sudo --remove-timestamp]' \
'-k[sudo --reset-timestamp]' \
'-n[sudo --non-interactive]' \
'-P[sudo --preserve-groups]' \
'-a+[sudo specify type]:type:()' \
'-C+[sudo specify close-from]:file descriptor:(4)' \
'-c+[sudo specify class]:class:()' \
'-g+[sudo specify group]:group:_groups' \
'-r+[sudo specify rule]:rule:()' \
'(1 * -)-V[Print version]' \
'(1 * -)-h[Print help]' \
'1:user name:_users' \
'*:::command: _normal'
local ret=$?
case $state in
(var)
	local vars
	vars=(
	{{,LS_}COLORS,DISPLAY,HOSTNAME,LS_COLORS,PS{1,2},TERM,XAUTHORI{TY,ZATION}}':passed by sudo default'
	'-:pass via command line'
	{COLORTERM,LANG{,UAGE},LC_{ALL,CTYPE,NUMERIC,TIME,COLLATE,MONETARY,MESSAGES,PAPER,NAME,ADDRESS,TELEPHONE,MEASUREMENT,IDENTIFICATION,ALL},LINGUAS,TERM,TZ}':checked by sudo default'
	)
	_describe -t variables 'variable' vars && ret=0;;
(fd)
	local fds
	fds=(
	'3'{,',4'}':file descriptor 3 (auxiliary descriptor 4)'
	'0:file descriptor 0, closing stdin'
	'0,0:file descriptor 0, pipe with a cat process'
	'-:pass via FIFO'
	)
	_describe -t 'file descriptors' 'file descriptor' fds && ret=0;;
esac
return ret
